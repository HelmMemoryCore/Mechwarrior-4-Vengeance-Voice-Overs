
//------------------------------------------------------------------
// NOTE: The fsm name below MUST be changed in order for the
// script to be considered a unique entity!

fsm Generic_Patrol : integer;


//------------------------------------------------------------------

// Generic_Patrol:
//   Follows a path.  If anything comes near, it goes off and attacks it,
//   and then comes back to the path when there's nothing left to attack.

//------------------------------------------------------------------



//------------------------------------------------------------------
//     Constants
//------------------------------------------------------------------

	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//     Types
//------------------------------------------------------------------

	type
		#include_ <content\ABLScripts\mwtype.abi>
	

//------------------------------------------------------------------
//     Variables
//------------------------------------------------------------------

	var
		static integer			attackRange;		// At what range do I start shooting?
		static integer			withdrawRange;		// At what range do I withdraw from combat entirely?

		static integer			path;				// My path
		static integer			moveType;			// How I move along the path
		static boolean			canLeavePath;		// Can I leave the path if attacked? (TRUE by default)

		static integer			piloting;			// Piloting skill
		static integer			gunnery;			// Gunnery skill/chance to hit
		static real				minDelay;			// Minimum amount of time I will wait between shots once a weapon is reloaded
		static real				maxDelay;			// Maximum amount of time I will wait between shots once a weapon is reloaded
		static integer 			eliteLevel;			// Elite level.  This helps determine tactics, defensive manuevers, opportunity fire
													// and other things.  It indicates a general level of combat experience.
		static integer			attackThrottle;		// My attack throttle
 		static integer			isShotRadius;		// How far away from me will I detect an enemy shot?
		static integer			findTypes;			// What kind of enemies to look for

		static integer 			attackSound;		// The attack sound I play when I first attack
		static integer 			deathSound;			// The sound I play when I die
		
		static integer			mood;				// My default mood.
		static integer			speed;				// The speed at which I move along the path

		static integer			takeOffDistance;	// My take-off distance if I'm an airplane (ignored if not an airplane)
		static integer			numdead;
		static integer			tactic;
		
		
//------------------------------------------------------------------
//     Init: my initialization function
//------------------------------------------------------------------

function Init;
	code
		// script-specific variables
		attackRange		= 500;
		withdrawRange	= 1000;
		path			= -1;
		moveType		= move_circle;
		canLeavePath	= true;
		speed			= 600;
		takeOffDistance	= 150;

		// driver settings
		piloting		= 70;
		gunnery			= 85;
		minDelay		= 0.1;
		maxDelay		= 0.3;
		eliteLevel		= 80;
		attackThrottle	= 80;
		isShotRadius	= 120;
		attackSound		= -1; // no sound
		deathSound		= -1; // no sound
		mood			= NEUTRAL_START;
		findTypes		= FT_DEFAULT;
		numdead = 25;
		tactic=tactic_pick_best;
		
	switch (getmemoryinteger(me,9))

	case 1:
		attackrange = 700;
		numdead = 2;
		path = epa_catpath;
	endcase;
						
	case 2:
		path = epa_basepath;
		attackrange = 650;
		numdead = 0;
		tactic = tactic_fast_circle;
	endcase;
	
	case 3:
		path = epa_vulturepath;
		attackrange = 650;
		numdead = 4;
	endcase;

	case 4:
		path = epa_ChopperPath;
		gunnery			= 65;
		attackrange = 550;
		numdead = 2;
	endcase;

	case 5:
		path = epa_group_one_patrol_id;
		attackrange = 650;
		gunnery			= 65;
		tactic = tactic_fast_circle;
	endcase;

	case 6:
		path = epa_group_seven_patrol_id;
		attackrange = 650;
		gunnery			= 65;
		tactic = tactic_fast_circle;
	endcase;

	case 7:
		path = epa_spawnpath;
		attackrange = 600;
		gunnery			= 65;
		numdead = 4;
	endcase;

	case 8:
		path = epa_airpath1;
		attackrange = 550;
		gunnery			= 65;
		numdead = 6;
	endcase;
	
		case 9:
		path = epa_longpath;
		attackrange = 650;
		gunnery			= 65;
		tactic = tactic_fast_circle;
	endcase;
	
	endswitch;


endfunction;

//------------------------------------------------------------------
//	StartState: my initial state
//------------------------------------------------------------------

state StartState;

	code
		SetFiringDelay			(ME,minDelay,maxDelay);
		SetIgnoreFriendlyFire	(ME,true);
		SetIsShotRadius			(ME,isShotRadius);
		SetEntropyMood			(ME,mood);
		SetCurMood				(ME,mood);
		SetSkillLevel			(ME,piloting,gunnery,eliteLevel);
		SetAttackThrottle		(ME,attackThrottle);

		if (orderTakeOff(takeOffDistance) == true) then
			trans FollowPathState;
		endif;

endstate;			

//------------------------------------------------------------------
//	FollowPathState: follow our path, but keep an eye out for enemies
//------------------------------------------------------------------

state FollowPathState;
	code
		
		if (GroupNumDead(GroupObjectId(19)) > numdead) then
		  trans gettoplayer;
		endif;		
		
		if (FindEnemy(attackRange,findTypes)) then
			trans AttackState;
		endif;

		if (path <> -1) then
			OrderMoveResumePatrol(path,speed,moveType,true,false);
		else
			OrderMoveLookOut;
		endif;
		
endstate;

state Gettoplayer;
	code
		
		ordermovetoobject(playermech,600); 	
		
		if (FindEnemy(attackRange,findTypes)) then
			trans AttackState;
		endif;
		
endstate;

//------------------------------------------------------------------
//	AttackState: attack my current target, or return to path if done
//------------------------------------------------------------------

state AttackState;
	code
		if (LeaveAttackState(withdrawRange)) then
			OrderStopAttacking;
			SetTarget(ME,NO_UNIT);
			trans FollowPathState;
		endif;

		OrderAttacktactic(tactic,canLeavePath);

endstate;

//------------------------------------------------------------------
//	DeadState: OK, I kicked the bucket.
//------------------------------------------------------------------

state DeadState;
	code	

		orderDie;

endstate;


endfsm.

