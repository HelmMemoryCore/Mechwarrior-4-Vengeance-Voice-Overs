
//*********************************************************************************
//The below line MUST be changed in order for the script to be considered
//a unique entity.  The word Patrol must be changed to some other unique identifier
fsm Patrol : integer;

//This unit patrols a specific path and attacks anything that comes near it.
//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
	type
		#include_ <content\ABLScripts\mwtype.abi>
	

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
	static integer			attackrange; // When do I start shooting?
																
	static integer			piloting; 	// piloting skill
	static integer			gunnery; // hit chance,
	static real					mindelay;		//minimum amount of time I will wait between shots once a weapon is reloaded
	static real					maxdelay;		//maximum amount of time I will wait between shots once a weapon is reloaded
	static integer 			speed;			 // What speed so I move at?
	static integer 			elitelevel; // % elite chance, This helps determine tactics, defensive manuevers, opportunity fire
																	// and other things that would normally be determined by combat experience

 	static integer			isshotradius;	//how far away from me will I detect an enemy shot.
	static integer			movetype;		//how will it move down the path?

	static Boolean 			attacksoundplayed;
	static integer 			attacksound; // plays when it first attacks
	static integer 			deathsound; // plays when it dies
	
	static integer			mood;	//What is my normal mood?
	static objectid			myfactory;
	
	static Boolean			reachedendofpath;	//Have I gotten to the end of the path?
																			//Note: This only gets set if movetype is set to move_nocycle.
	static integer			path;		 //what path am I following?
	static Boolean 			attackersdistractit;	// if FALSE, it won't leave the path, even when things
		// are shooting it. It will shoot back. Defaults to true
// Once unit reaches the end of the path, of course, it attacks freely.
	
//------------------------------------------------------------------
//
//     Local Functions
//
//------------------------------------------------------------------

function init;
	code
	
																					// defaults are below
	//These are the variables that are normally changed by the designer
	//in order to change behavior
	attackrange = 300;											// 500
	piloting = 65;														//50
	gunnery = 75;												//30
	mindelay = 0.1;													//1.5
	maxdelay = 0.5;													//3.0
	elitelevel = 74;												//30
	speed = 600;														//600 (max speed)	
	isshotradius = 80;												//80 	
	attacksound = -1;                   //-1 (no sound)
	deathsound = -1;                    //-1 (no sound)
	path = -1;													 //-1 (no path)
	movetype = move_circle;									//move_circle
	attackersdistractit = TRUE;					 //true
	mood = neutral_start;								//neutral_start
			
 	//These variables should not be changed by the designer without good reason
	reachedendofpath = FALSE;						 //false
	attacksoundplayed = FALSE;									//false
	
	myfactory = -1;

	
	//Use this to set different values for different units using the same brain	
	switch (GetMemoryInteger(me,9))

		case 1:
		
		attackrange = 250;
	  piloting = 65;							
		gunnery = 75;						 					
		speed = 80;							 	
		isshotradius = 40;						 	
		attacksound = -1;
		deathsound = -1;
		path = epa_patrolpath3;
		movetype = move_circle;
		attackersdistractit = TRUE;
		mood = neutral_start;
		myfactory = ebu_munitions_plant_1;
				
		endcase;
		
		case 2:
		
		attackrange = 250;
	  piloting = 30;												
		speed = 80;							 	
		isshotradius = 30;						 	
		attacksound = -1;
		deathsound = -1;
		path = epa_patrolpath2;
		movetype = move_circle;
		attackersdistractit = TRUE;
		mood = neutral_start;
		myfactory =	ebu_munitions_plant_2;
				
		endcase;
		
		case 3:
		
		attackrange = 250; 					
		speed = 80;							 	
		isshotradius = 30;						 	
		attacksound = -1;
		deathsound = -1;
		path = -1;
		movetype = move_circle;
		attackersdistractit = TRUE;
		mood = neutral_start;
		myfactory =	ebu_munitions_plant_3;
				
		endcase;
		
		case 4:
		
		attackrange = 200; 					
		speed = 80;							 	
		isshotradius = 60;						 	
		attacksound = -1;
		deathsound = -1;
		path = epa_patrolpath1;
		movetype = move_circle;
		attackersdistractit = TRUE;
		mood = neutral_start;
				
		endcase;
		
	endswitch;

	
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code
		SetFiringDelay(me,mindelay,maxdelay);
		setignorefriendlyfire(me,true);
		setisshotradius(me,isshotradius);
		setentropymood(me,mood);
		setcurmood(me,mood);
		setskilllevel(me,piloting,gunnery,elitelevel);
		//setsearchlight(me,on);
		//startexecute(me);
		//setalignment(me,enemy);
				
		trans patrol;
endstate;			

state deadState;
  code
		if (deathsound <> -1) then	
			PlaySoundOnce(deathsound);
		endif;		
						
		orderDie;
endstate;

	state patrol;

  var
    ObjectID foe;

		code

		orderMoveto(path,speed,movetype,true,true);
		
		if (myfactory <> -1) then
			if (isdead(myfactory)) then
				attackrange = 500;
			endif;
		endif;
			
		//Find me a target
		if (setmytarget(attackrange)) then
			attackrange = 500;
			trans attack;
	 	endif;
			
	endstate;

//------------------------------------------------------------------
//
//     attack state : description
//
//------------------------------------------------------------------
	state attack;
	
		
		code

			
		if (LeaveAttackState(attackrange*3)) then
			orderstopattacking;
			orderMoveResumePatrol(path,speed,movetype,true,true);
			attackrange = 350;
			trans patrol;
		endif;
		
		//the ever popular attack order
    orderAttack (attackersdistractit);
		
		//play the attack sound if one exists
		if ((attacksound <> -1) and (attacksoundplayed == FALSE)) then	
			attacksoundplayed = true;
			PlaySoundOnce(attacksound);
		endif;	
		
		//maybe swap targets when I am shot.
		if ((isShot (me)) and (rand(0,25) <= 1)) then
  		setTarget(me,whoShot(me));
		endif;
 			
					
	endstate;


endfsm.
//******************************************************************

