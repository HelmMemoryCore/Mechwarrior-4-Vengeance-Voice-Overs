
//------------------------------------------------------------------
// NOTE: The fsm name below MUST be changed in order for the
// script to be considered a unique entity!

fsm Patrol : integer;


//------------------------------------------------------------------

// Generic_Patrol:
//   Follows a path.  If anything comes near, it goes off and attacks it,
//   and then comes back to the path when there's nothing left to attack.

//------------------------------------------------------------------



//------------------------------------------------------------------
//     Constants
//------------------------------------------------------------------

	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//     Types
//------------------------------------------------------------------

	type
		#include_ <content\ABLScripts\mwtype.abi>
	

//------------------------------------------------------------------
//     Variables
//------------------------------------------------------------------

	var
		static integer			attackRange;		// At what range do I start shooting?
		static integer			withdrawRange;		// At what range do I withdraw from combat entirely?

		static integer			path;				// My path
		static integer			moveType;			// How I move along the path
		static boolean			canLeavePath;		// Can I leave the path if attacked? (TRUE by default)

		static integer			piloting;			// Piloting skill
		static integer			gunnery;			// Gunnery skill/chance to hit
		static real				minDelay;			// Minimum amount of time I will wait between shots once a weapon is reloaded
		static real				maxDelay;			// Maximum amount of time I will wait between shots once a weapon is reloaded
		static integer 			eliteLevel;			// Elite level.  This helps determine tactics, defensive manuevers, opportunity fire
													// and other things.  It indicates a general level of combat experience.
		static integer			attackThrottle;		// My attack throttle

 		static integer			isShotRadius;		// How far away from me will I detect an enemy shot?

		static integer 			attackSound;		// The attack sound I play when I first attack
		static integer 			deathSound;			// The sound I play when I die
		
		static integer			mood;				// My default mood.
		static integer			speed;				// The speed at which I move along the path
		static integer			mytactic;
		static boolean			playedit;

//------------------------------------------------------------------
//     Init: my initialization function
//------------------------------------------------------------------

function Init;
	code
		// script-specific variables
		attackRange		= 500;
		withdrawRange	= 1100;
		path			= -1;
		moveType		= move_circle;
		canLeavePath	= true;
		speed			= 600;
		playedit = true;

		// driver settings
		piloting		= 55;
		gunnery			= 58;
		minDelay		= 1.0;
		maxDelay		= 2.5;
		eliteLevel		= 68;
		attackThrottle	= 80;
		isShotRadius	= 80;
		attackSound		= -1; // no sound
		deathSound		= -1; // no sound
		mood			= NEUTRAL_START;
		mytactic = tactic_circle;
		
		
		switch (getmemoryinteger(me,9))

	case 1:
		//AlphaMechs
		attackrange = 650;
		mytactic = tactic_rear;
		path = epa_Alphapath;
		mood = AGRESSIVE_START;
		attackthrottle = 50;
	endcase;
						
	case 2:
		//BetaMechs
		attackrange = 700;
		mytactic = tactic_circle;
		path = epa_Betapath;
		mood = DEFENSIVE_START;
		attackthrottle = 40;
	endcase;
	
	case 3:
		//GammaMechs
		attackrange = 700;
		mytactic = tactic_Rush;
		path = epa_gammapath;
		attackthrottle = 65;
	endcase;

	case 4:
		//DeltaMechs
		attackrange = 800;
		mytactic = tactic_pick_best;
		path = epa_deltapath;
		minDelay		= 1.5;
		maxDelay		= 3.5;
		mood = AGRESSIVE_START;
		gunnery			= 48;
		playedit = false;
	endcase;
	
	
	endswitch;



endfunction;

//------------------------------------------------------------------
//	StartState: my initial state
//------------------------------------------------------------------

state StartState;

	code
		SetFiringDelay			(ME,minDelay,maxDelay);
		SetIgnoreFriendlyFire	(ME,true);
		SetIsShotRadius			(ME,isShotRadius);
		SetEntropyMood			(ME,mood);
		SetCurMood				(ME,mood);
		SetSkillLevel			(ME,piloting,gunnery,eliteLevel);
		SetAttackThrottle		(ME,attackThrottle);
				
		trans FollowPathState;

endstate;			

//------------------------------------------------------------------
//	FollowPathState: follow our path, but keep an eye out for enemies
//------------------------------------------------------------------

state FollowPathState;
	code
		if (SetMyTarget(attackRange)) then
			trans AttackState;
		endif;

		if (path <> -1) then
			OrderMoveResumePatrol(path,speed,moveType,true,false);
		else
			OrderMoveLookOut;
		endif;
endstate;

//------------------------------------------------------------------
//	AttackState: attack my current target, or return to path if done
//------------------------------------------------------------------

state AttackState;
	code
		if (LeaveAttackState(withdrawRange)) then
			OrderStopAttacking;
			SetTarget(ME,NO_UNIT);
			trans FollowPathState;
		endif;

		if (not playedit) then
			playedit = true;
			StartMusic("Music\Wednesday7",2.5,5.0,80);
		endif;


		OrderAttacktactic(Mytactic,canLeavePath);

endstate;

//------------------------------------------------------------------
//	DeadState: OK, I kicked the bucket.
//------------------------------------------------------------------

state DeadState;
	code
		if (deathSound <> -1) then	
			PlaySoundOnce(deathSound);
		endif;		

		orderDie;

endstate;


endfsm.

