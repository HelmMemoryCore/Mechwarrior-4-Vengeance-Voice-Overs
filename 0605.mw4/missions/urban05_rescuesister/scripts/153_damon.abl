//------------------------------------------------------------------
fsm Squire : integer;


//------------------------------------------------------------------
//     Constants
//------------------------------------------------------------------

	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//     Types
//------------------------------------------------------------------

	type
		#include_ <content\ABLScripts\mwtype.abi>
	

//------------------------------------------------------------------
//     Variables
//------------------------------------------------------------------

	var
		static integer			attackRange;		// At what range do I start attacking?
		static integer			withdrawRange;		// At what range do I withdraw from combat?
		static integer			groupNumber;		// What's my group -- i.e. I will use GroupObjectID(groupNumber) to identify my group
		static integer			speed;				// The speed at which I move along the path
		static integer			formationType;		// What kind of formation am I in?
		static integer			formationDensity;	// How densely are we packed?
		static integer			path;				// My path
		static integer			moveType;			// How I move along the path

		static integer			piloting;			// Piloting skill
		static integer			gunnery;			// Gunnery skill/chance to hit
		static real				minDelay;			// Minimum amount of time I will wait between shots once a weapon is reloaded
		static real				maxDelay;			// Maximum amount of time I will wait between shots once a weapon is reloaded
		static integer 			eliteLevel;			// Elite level.  This helps determine tactics, defensive manuevers, opportunity fire
													// and other things.  It indicates a general level of combat experience.
		static integer			attackThrottle;		// My attack throttle

 		static integer			isShotRadius;		// How far away from me will I detect an enemy shot?
		static integer			findTypes;			// What kind of enemies to look for

		static integer 			attackSound;		// The attack sound I play when I first attack
		static integer 			deathSound;			// The sound I play when I die

		static integer			mood;				// My default mood.

		static integer			friendlyFireFrustrationLevel;
		static integer			frustration_timer;
		static integer			attack_player_timer;

		static boolean			InitialMove;
		static boolean			CheckSister;
		Static Boolean			PathCheck;
		
//------------------------------------------------------------------
//     Init: my initialization function
//------------------------------------------------------------------

function Init;
	code
		// script-specific variables
		
		PathCheck = False;
		attackRange		= 800;
		withdrawRange	= attackRange * 3 / 2;
		groupNumber		= 1;
		path			= -1;
		moveType		= move_nocycle;
		speed			= 42;
		formationType	= 1;
		formationDensity= formtype_sparse;
		CheckSister = false;


		// driver settings
		piloting		= 90;
		gunnery			= 90;
		minDelay		= 0.0;
		maxDelay		= 0.5;
		eliteLevel		= 90;
		attackThrottle	= 100;
		isShotRadius	= 160;
		attackSound		= -1; // no sound
		deathSound		= -1; // no sound
		mood			= NEUTRAL_START;
		findTypes		= FT_DEFAULT;

		friendlyFireFrustrationLevel	= 0;
		frustration_timer				= gti_Timer_12;
		attack_player_timer				= gti_Timer_13;

		InitialMove = True;

		StartTimer(frustration_timer);
		SetTimer(frustration_timer,100);

endfunction;

function CheckIfPlayerShotMe : integer;
	var
		ObjectID who_shot;

	code
		if (Not TimeGreater(frustration_timer,8)) then
			return (0);
		endif;
		
		if (Not IsShot(ME)) then
			return (0);
		endif;

		who_shot = WhoShot(me);

		if (who_shot <> getplayervehicle(epl_player0)) then
			return (0);
		endif;

		ResetTimer(frustration_timer);

		if (friendlyFireFrustrationLevel == 0) then
			PlayVoiceOverOnce("VO\Generic\CheckFire1",TALKER_DAM);
			friendlyFireFrustrationLevel = 1;
		else
			if (friendlyFireFrustrationLevel == 1) then
				PlayVoiceOverOnce("VO\Generic\CheckFire2",TALKER_DAM);
				friendlyFireFrustrationLevel = 2;
			else
				if (friendlyFireFrustrationLevel == 2) then
					PlayVoiceOverOnce("VO\Generic\CheckFire1",TALKER_DAM);
					friendlyFireFrustrationLevel = 3;
				else
					PlayVoiceOverOnce("VO\Generic\KillIan1",TALKER_RAT);
					StartTimer(attack_player_timer);
					ResetTimer(attack_player_timer);

					SetGlobalTrigger(77,true);
					trans KillPlayerState;
				endif;
			endif;
		endif;

		return (1);

endfunction;

//------------------------------------------------------------------
//	StartState: my initial state
//------------------------------------------------------------------

state StartState;

	code
		SetFiringDelay			(ME,minDelay,maxDelay);
		SetIgnoreFriendlyFire	(ME,false);
		SetIsShotRadius			(ME,isShotRadius);
		SetEntropyMood			(ME,mood);
		SetCurMood				(ME,mood);
		SetSkillLevel			(ME,piloting,gunnery,eliteLevel);
		SetAttackThrottle		(ME,attackThrottle);
		trans Wait;

endstate;

//------------------------------------------------------------------
//	FollowPathState: follow our path, but keep an eye out for enemies
//------------------------------------------------------------------

state Wait;

	code
	
		CheckIfPlayerShotMe;

	
		if (FindEnemy(attackRange,FT_DEFAULT)) then
			trans AttackState1;
		endif;

		OrderMoveFollow(playermech,0,-30);

		if GetGlobalTrigger(97) == true then
			trans Setup;
		endif;
		
endstate;

State Setup;
	code
			IF PathCheck == false then
				if OrderMoveTo(epa_HoldingPath,600,Move_NoCycle,false,false) == true then
					PathCheck = true;
					trans FOLLOW;
				endif;
			endif;
	
							
endState;

state Follow;

	code
		
		CheckIfPlayerShotMe;

		
		if (FindEnemy(attackRange,FT_DEFAULT)) then
			trans AttackState2;
		endif;

		OrderMoveFollow(eve_joanna,0,-30);

	
endState;


//------------------------------------------------------------------
//	AttackState: attack my target
//------------------------------------------------------------------

state AttackState1;
	code
		CheckIfPlayerShotMe;

		if (LeaveAttackState(withdrawRange)) then
			OrderStopAttacking;
			SetTarget(ME,NO_UNIT);
			trans wait;
		endif;

		if (attackSound <> -1) then	
			PlaySoundOnce(attackSound);
		endif;

		OrderAttackTactic(TACTIC_PICK_BEST,false);

		if CHECKSISTER == false then
			if GetGlobalTrigger(97) == true then
				CheckSister = true;
					if OrderMoveTo(epa_HoldingPath,600,Move_NoCycle,false,false) == true then
						trans FOLLOW;
					endif;
			endif;
		endif;


endstate;

state AttackState2;
	code
		CheckIfPlayerShotMe;

		if (LeaveAttackState(withdrawRange)) then
			OrderStopAttacking;
			SetTarget(ME,NO_UNIT);
			trans follow;
		endif;

		if (attackSound <> -1) then	
			PlaySoundOnce(attackSound);
		endif;

		OrderAttackTactic(TACTIC_PICK_BEST,false);


endstate;


//------------------------------------------------------------------
//	KillPlayerState: attack the player
//------------------------------------------------------------------

state KillPlayerState;
	code
		if (TimeGreater(attack_player_timer,4)) then
			SetFiringDelay			(ME,0.0,0.0);
			SetSkillLevel			(ME,100,500,100);

			SetAutoTargeting(ME,FALSE);
			SetTarget(ME,getplayervehicle(epl_player0));
			OrderAttackTactic(TACTIC_Hit_AND_RUN,TRUE);
		endif;

endstate;

//------------------------------------------------------------------
//	DeadState: OK, I kicked the bucket.
//------------------------------------------------------------------

state DeadState;
	code
	
	
		PlayVoiceOverOnce("VO\GENERIC\Ejecting1",Talker_DAM);	
		
		orderDie;

endstate;


endfsm.

