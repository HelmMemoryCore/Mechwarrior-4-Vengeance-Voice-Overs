
//*********************************************************************************
//The below line MUST be changed in order for the script to be considered
//a unique entity.  The word Patrol must be changed to some other unique identifier
fsm TankPatrol : integer;

//This unit patrols a specific path and attacks anything that comes near it.
//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
	type
		#include_ <content\ABLScripts\mwtype.abi>
	

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
	static integer			attackrange; 		// When do I start shooting?
																
	static integer			piloting;  		 	// piloting skill
	static integer			gunnery; 				// hit chance,
	static real					mindelay;				//minimum amount of time I will wait between shots once a weapon is reloaded
	static real					maxdelay;	 		 	//maximum amount of time I will wait between shots once a weapon is reloaded
	static integer 			speed;				 	// What speed so I move at?
	static integer 			elitelevel;			// % elite chance, This helps determine tactics, defensive manuevers, opportunity fire
																			// and other things that would normally be determined by combat experience

 	static integer			isshotradius;		//how far away from me will I detect an enemy shot.
	static integer			movetype;				//how will it move down the path?

	static Boolean 			Secret;					//They are Powered down in Ambush
	static Boolean 			Waiters;					//They are Powered down in Ambush
	static Boolean 			attacksoundplayed;
	static integer 			attacksound; 		// plays when it first attacks
	static integer 			deathsound;		 	// plays when it dies
	
	static integer			mood;						//What is my normal mood?
	
	static Boolean			reachedendofpath;	//Have I gotten to the end of the path?
																			 //Note: This only gets set if movetype is set to move_nocycle.
	static integer			Base;						 //Where is my Base?  Am i too good for my HOME?
	static integer			path;						 //what path am I following?
	static Boolean 			attackersdistractit;	// if FALSE, it won't leave the path, even when things
																						// are shooting it. It will shoot back. Defaults to true
																						// Once unit reaches the end of the path.  It attacks freely.
	
//------------------------------------------------------------------
//
//     Local Functions
//
//------------------------------------------------------------------

function init;
	code
	
	//These variables should not be changed by the designer without good reason
	reachedendofpath = FALSE;						 				//false
	attacksoundplayed = FALSE;									//false
	Secret = FALSE;															//They are not in hiding
	Waiters = FALSE;
	
	//Use this to set different values for different units using the same brain	
	switch (GetMemoryInteger(me,9))

		case 1:															 //					Tanks on Ambush
		
		attackrange = 900;				
		piloting = 50;			//70 					
	  gunnery = 35;				//25				 was 45
		mindelay = 0.5;			//.5				
		maxdelay = 1.0;			//2						
		elitelevel = 50;		//50				
	  speed = 80;					//100			 	
		isshotradius = 200;						 	
		attacksound = -1;
		deathsound = -1;
		path = -1;
		movetype = move_nocycle;
		Secret = TRUE;
		attackersdistractit = TRUE;
		mood = neutral_start;
		
		endcase;
		
		case 2:																 //				Tanks Gaurding RidgeLine
				
		attackrange = 600;				
		piloting = 50;			//70				
	  gunnery = 40;				//20				was 70
		mindelay = 0.75;		//1				
		maxdelay = 1.0;			//2.5						
		elitelevel = 50;		//50				
	  speed = 100;				//100			 	
		isshotradius = 80;						 	
		attacksound = -1;
		deathsound = -1;
		path = epa_CityTanksPath;
		movetype = move_circle;
		Waiters = TRUE;
		attackersdistractit = TRUE;
		mood = neutral_start;
		
		endcase;
		
		case 3:																 //				Tanks Gaurding Bowl
				
		attackrange = 550;				
		piloting = 50;			//70				
	  gunnery = 45;				//20						was 70
		mindelay = 0.75;		//1				
		maxdelay = 1.0;			//2.5						
		elitelevel = 50;		//50				
	  speed = 100;				//100			 	
		isshotradius = 80;						 	
		attacksound = -1;
		deathsound = -1;
		path = epa_STankPath;
		movetype = move_circle;
		attackersdistractit = FALSE;
		mood = neutral_start;
		
		endcase;
		
		
	endswitch;

	
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code
		
		SetFiringDelay(me,mindelay,maxdelay);
		setignorefriendlyfire(me,true);
		setisshotradius(me,isshotradius);
		setentropymood(me,mood);
		setcurmood(me,mood);
		setskilllevel(me,piloting,gunnery,elitelevel);
		//setsearchlight(me,on);
		//startexecute(me);
		//setalignment(me,enemy);
		if (secret == true) then
			shutdown(me);
			trans hangout;
		else
			trans patrol;
		endif;

endstate;			

state deadState;
  code
		if (deathsound <> -1) then	
			PlaySoundOnce(deathsound);
		endif;
		
		If (Waiters == true) then
				Engaged = True;
		endif;		
						
		orderDie;
endstate;

state Hangout;
  code

				If ((isShot (me)))	then
					startup(me);
					trans patrol;
				endif;
				
				if ((iswithin(me,(groupobjectid(1)),300)) == true) then
					startup(me);
					trans patrol;
				endif;
						
				If (Engaged == true) then
					startup (me);
					trans patrol;
				endif;

endstate;

	state patrol;

  var

		ObjectID foe;

		code

	
	//head down the path.  If this has already been done
	// or if there is no path set then look around.
			if ((path <> -1) and (reachedendofpath == FALSE)) then
					if ((orderMoveto(path,speed,movetype,true,true)) == TRUE) then
						reachedendofpath = TRUE;
					endif; 			
			else	 		 		
				OrderMoveToObject((playermech),200);			
				path = epa_STankPath;
			endif;
			
		//Find me a target
			if (setmytarget(attackrange)) then
				trans attack;
			endif;
			
	endstate;

//------------------------------------------------------------------
//
//     attack state : description
//
//------------------------------------------------------------------
	state attack;
	
		
		code

			
		if (LeaveAttackState(attackrange*3/2)) then
			orderstopattacking;
			orderMoveResumePatrol(path,speed,movetype,true,attackersdistractit);
			trans patrol;
		endif;
		
		//the ever popular attack order
    orderAttack (attackersdistractit);
		
		//if the ridge tanks are in a fight, then the ambush is set
		If (Waiters == true) then
				Engaged = True;
				Waiters = False;
		endif;
		
		//play the attack sound if one exists
		if ((attacksound <> -1) and (attacksoundplayed == FALSE)) then	
			attacksoundplayed = true;
			PlaySoundOnce(attacksound);
		endif;	
		
		//maybe swap targets when I am shot.
		if ((isShot (me)) and (rand(0,75) <= 1)) then
  		setTarget(me,whoShot(me));
		endif;
 			
					
	endstate;


endfsm.
//******************************************************************

