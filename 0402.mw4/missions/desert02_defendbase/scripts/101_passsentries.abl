
//*********************************************************************************
//The below line MUST be changed in order for the script to be considered
//a unique entity.  The word Sentry must be changed to some other unique identifier
fsm PassSentries : integer;
		


// 		Behavior: Sits stationary and looks around. When an enemy comes within
//   alertrange it moves towards the unit
//	When the unit comes within attackrange it attacks
//	Whenit kills the unit it can either go back to it's start point or not,
//	depending on different settings in the brain.


//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
	const
		#include_ <content\ABLScripts\mwconst.abi>


//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
	type
		#include_ <content\ABLScripts\mwtype.abi>
	

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
	var
	static integer			attackrange; // When do I start shooting? 															
	static integer			piloting; 	// piloting skill
	static integer			gunnery; // hit chance,
	static real					mindelay;		//minimum amount of time I will wait between shots once a weapon is reloaded
	static real					maxdelay;		//maximum amount of time I will wait between shots once a weapon is reloaded
	static integer 			speed;			 // What speed so I move at?
	static integer 			elitelevel; // % elite chance, This helps determine tactics, defensive manuevers, opportunity fire
																	// and other things that would normally be determined by combat experience

  static integer			isshotradius;	//how far away from me will I detect an enemy shot.
	
	static integer			mood;	//What is my normal mood?
			
	static Boolean 			attacksoundplayed;	//Am I attacking?
	
	//if set to -1 the below sounds effectively do not exist
	static integer 			attacksound; // plays when the unit first attacks
	static integer 			deathsound; // plays when the unit dies
	
	static integer			xoffset;
	static integer			yoffset;
	static boolean			linkedup;
	static integer			mypath;

	static integer			friendlyFireFrustrationLevel;
	static integer			frustration_timer;
	static integer			attack_player_timer;
	
	
//------------------------------------------------------------------
//
//     Local Functions
//
//------------------------------------------------------------------

function init;
	code

																			// defaults are below
	//These are the variables that are normally changed by the designer
	//in order to change behavior
	attackrange = 500;									//500
	piloting = 60;												//50
	gunnery = 75;											//30
	mindelay = 0.1;											//1.5
	maxdelay = 0.2;											//3.0
	elitelevel = 75;										//30
	speed = 600;												//600 (max speed)
	isshotradius = 80;										//80	
	attacksound = -1;                   //-1 (no sound)
	deathsound = -1;                    //-1 (no sound)	
	mood = neutral_start;								//neutral_start

	//These variables should not be changed by the designer without good reason
	attacksoundplayed = FALSE;									//false
	linkedup = false;
	mypath = -1;
	xoffset = 0;
	yoffset = 0;
	
	friendlyFireFrustrationLevel	= 0;

	frustration_timer				= gti_Timer_10;
	attack_player_timer				= gti_Timer_11;
	
	//Use this to set different values for different units using the same brain
	switch (GetMemoryInteger(me,9))

		//osiris
		case 0:
		
		mypath = epa_passguardpath;
		xoffset = -30;
		yoffset = -20;

		frustration_timer				= gti_Timer_12;
		attack_player_timer				= gti_Timer_13;
		
		endcase;
		
		//bulldog1
		case 1:
		
		xoffset = -40;
		yoffset = 30;
		 	
		frustration_timer				= gti_Timer_14;
		attack_player_timer				= gti_Timer_15;

		endcase;
		
		//bulldog
		case 2:
		
		xoffset = -40;
		yoffset = -30;
		 	
		frustration_timer				= gti_Timer_16;
		attack_player_timer				= gti_Timer_17;

		endcase;
		
		//catapult
		case 3:
		
		xoffset = -30;
		yoffset = 20;
		//attacksound = eso_De02Warning;
		 	
		frustration_timer				= gti_Timer_18;
		attack_player_timer				= gti_Timer_19;

		endcase;
		
	endswitch;

	StartTimer(frustration_timer);
	SetTimer(frustration_timer,100);
	
endfunction;


//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code
		SetFiringDelay(me,mindelay,maxdelay);
		setignorefriendlyfire(me,true);
		setisshotradius(me,isshotradius);
		setentropymood(me,mood);
		setcurmood(me,mood);
		setskilllevel(me,piloting,gunnery,elitelevel);
		//setsearchlight(me,on);
		//startexecute(me);
		setalignment(me,friendly);

	
		trans sit;
endstate;			

state deadState;
  code
		if (deathsound <> -1) then	
			PlaySoundOnce(deathsound);
		endif;
		
		orderDie;
endstate;

	state sit;

		code
			
	 	if (mypath == -1) then
			OrderMoveLookout;
		else
			ordermoveto(mypath,50,move_circle,true,true);
		endif;
				
		if (setmytarget(attackrange)) then
			trans attack;
	 	endif;
		
		if (not linkedup) then
			if (linkupobjectivedone) then
				//playsoundonce
				linkedup = true;
				attackrange = 650;
				trans follow;
			endif;
		endif;
					
	endstate;

	state follow;

		code
		
		orderMoveFollow(playermech,xoffset,yoffset);
		
		if (setmytarget(attackrange)) then
			trans attack;
	 	endif;
					
	endstate;

//------------------------------------------------------------------
//
//     attack state : description
//
//------------------------------------------------------------------
	state attack;
	 	var
		
		code
		
		//Get out of the attack state if I should	
		if (LeaveAttackState(attackrange*3/2)) then
			orderstopattacking;
			if (linkupobjectivedone) then			
				trans follow;
			else
				trans sit;
			endif;
		endif;
		
			//Attack
			OrderAttack(true);
			
		//play the attack sound if one exists
		if ((attacksound <> -1) and (attacksoundplayed == FALSE)) then	
			attacksoundplayed = true;
			PlaySoundOnce(attacksound);
		endif;
		
		//maybe swap targets when I am shot.
		if ((isShot (me)) and (rand(0,75) <= 1)) then
  		setTarget(me,whoShot(me));
		endif;

	endstate;

//------------------------------------------------------------------
//	KillPlayerState: too much friendly fire -- get pissed and attack the player
//------------------------------------------------------------------

state KillPlayerState;
	code
		if (TimeGreater(attack_player_timer,6)) then
			SetFiringDelay			(ME,0.0,0.0);
			SetSkillLevel			(ME,100,500,100);

			SetAutoTargeting(ME,FALSE);
			SetTarget(ME,getplayervehicle(epl_player0));
			OrderAttack(TRUE);
		endif;
endstate;

	
endfsm.
//******************************************************************

