
//*********************************************************************************

fsm GENERIC : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer playermech;
		
		
		static integer	cinema_cut;
		
		eternal integer	gametimer;
		eternal boolean	billyup;
		eternal boolean Marcusbyebye;
		
		static boolean	elitedead;
		static boolean	dropshipdead;
		static boolean	billydead;
		
		eternal boolean	moviedone;
		
		static integer	numdone;
		static boolean	generatordead;
		eternal boolean	killtheplayer;
		static boolean	entrancesoundplayed;
		eternal boolean	boomboomstarted;
		static boolean	elitesoundplayed;
		static boolean	Genclosesoundplayed;
		static boolean	Dropclosesoundplayed;
		
		static boolean	pleasplayed;		
		static integer	dropshiparmor;
		static integer	armortime;
		static integer	armormult;
		static integer	pleastep;
		static boolean	skipit;
		
		//Navpoint Check Variables
		static integer	checkrange;
		static boolean	alphareached;
		static boolean	betareached;
		static boolean	gammareached;
		static boolean	deltareached;
		static boolean	epsilonreached;
		
		
		
 //------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code
	
			//Navpoint Check Declarations
		checkrange = 150;
		alphareached = false;
		betareached = false;
		gammareached = false;
		deltareached = false;
		epsilonreached = false;



		 playermech = getplayervehicle(epl_player0);	
 	
			GroupAddObject(GroupObjectId(1),playermech);


			GroupAddObject(GroupObjectId(10),eve_outerpatrol1);
			GroupAddObject(GroupObjectId(10),eve_outerpatrol2);
			SetGroupAI(groupobjectid(10),GROUPAI_RADIOSQUAD);
			
			GroupAddObject(GroupObjectId(11),eve_innerpatrol1);
			GroupAddObject(GroupObjectId(11),eve_innerpatrol2);
			SetGroupAI(groupobjectid(11),GROUPAI_RADIOSQUAD);
			
			GroupAddObject(GroupObjectId(12),eve_dropshippatrol1);
			GroupAddObject(GroupObjectId(12),eve_dropshippatrol2);
			SetGroupAI(groupobjectid(12),GROUPAI_RADIOSQUAD);	
			
			GroupAddObject(GroupObjectId(30),eve_Shutdown1);
			GroupAddObject(GroupObjectId(30),eve_Shutdown2);
			GroupAddObject(GroupObjectId(30),eve_dropshippatrol1);
			GroupAddObject(GroupObjectId(30),eve_dropshippatrol2);		
			GroupAddObject(GroupObjectId(30),eve_innerpatrol1);
			GroupAddObject(GroupObjectId(30),eve_innerpatrol2);
			GroupAddObject(GroupObjectId(30),eve_outerpatrol1);
			GroupAddObject(GroupObjectId(30),eve_outerpatrol2);

			GroupAddObject(GroupObjectId(20),eve_Shutdown1);
			GroupAddObject(GroupObjectId(20),eve_Shutdown2);
			GroupAddObject(GroupObjectId(20),eve_outerpatrol1);
			GroupAddObject(GroupObjectId(20),eve_outerpatrol2);
			GroupAddObject(GroupObjectId(20),eve_innerpatrol1);
			GroupAddObject(GroupObjectId(20),eve_innerpatrol2);
			GroupAddObject(GroupObjectId(20),eve_dropshippatrol1);
			GroupAddObject(GroupObjectId(20),eve_dropshippatrol2);
			
			
			SetTarget(ede_deerherd2,ede_deerherd1);
			SetTarget(ede_deerherd3,ede_deerherd1);
			SetTarget(ede_deerherd4,ede_deerherd1);
			SetTarget(ede_deerherd5,ede_deerherd1);
			SetTarget(ede_deerherd6,ede_deerherd1);
			SetTarget(ede_deerherd7,ede_deerherd1);
			SetTarget(ede_deerherd8,ede_deerherd1);
			SetTarget(ede_deerherd9,ede_deerherd1);
			SetTarget(ede_deerherd10,ede_deerherd1);
			SetTarget(ede_deerherd11,ede_deerherd1);
			SetTarget(ede_deerherd12,ede_deerherd1);


			SetTarget(ede_deerherd21,ede_deerherd20);
			SetTarget(ede_deerherd22,ede_deerherd20);
			SetTarget(ede_deerherd23,ede_deerherd20);
			SetTarget(ede_deerherd24,ede_deerherd20);
			SetTarget(ede_deerherd25,ede_deerherd20);
			SetTarget(ede_deerherd26,ede_deerherd20);

			
		cinema_cut = 0;
		gametimer = gti_timer_0; 	
		billyup = false;	
		marcusbyebye = false;		
		
		elitedead = false;
		dropshipdead = false;
		billydead = false;
		moviedone = false;
		numdone = 0;
		generatordead = false;
		killtheplayer = false;
		entrancesoundplayed = false;
		
		boomboomstarted = false;
		elitesoundplayed = false;
		Genclosesoundplayed = false;
		Dropclosesoundplayed = false;

		pleasplayed = false;				
		dropshiparmor = getdamagerating(eve_hrothgar);
		armortime = -1;
		armormult = 9;
		pleastep = 1;
		skipit = true;
		
			
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
			revealobjective(eob_stopeliteguard);
			revealobjective(eob_stopdropship);
			revealobjective(eob_destroygenerator);
			starttimer(gametimer);
			playvoiceover("VO\op7\MI1\intro",talker_Ian);
			playvoiceover("VO\op7\MI1\intro",talker_Rat);
			playvoiceover("VO\op7\MI1\intro2",talker_Ian);
			playvoiceover("VO\op7\MI1\intro2",talker_Rat);
			playvoiceover("VO\op7\MI1\intro3",talker_Ian);
			playvoiceover("VO\op7\MI1\intro3",talker_Rat);
			playvoiceover("VO\op7\MI1\intro4",talker_Ian);
			playvoiceover("VO\op7\MI1\Turrets",talker_FO1);
			setnavpoint(ena_Nav_Alpha);
			revealnavpoint(ena_Nav_Beta);
			revealnavpoint(ena_Nav_Gamma);
			
			Playerai(epl_player0,true);
			StartMusic("Music\daviontheme",0.0,3.0,60);
			trans opening;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code
	
				//PlayVoiceOverOnce("VO\Generic\Startup1",Talker_Bet);
				PlayVoiceOverOnce("VO\Generic\Startup2",Talker_Bet);
				PlayVoiceOverOnce("VO\Generic\Startup3",Talker_Bet);
				PlayVoiceOverOnce("VO\Generic\Startup6",Talker_Bet);

	
			
//Reveal the objectives

//Fail the mission when the player dies
	if (isdead(playermech)) then
		endmission(false,12);
		if (moviedone) then
			playvoiceover("VO\op7\MI1\iandead",talker_Wil);
		else
			killvoiceovers;
			PlayVoiceOver("VO\Generic\Iandestroyed",talker_rat);
		endif;
		StartMusic("Music\defeat",2.0,2.0,90);
		trans lost;
	endif;

	if (marcusbyebye) then
		endmission(false,19);
		playvoiceover("VO\op7\MI1\shiplift",talker_Rat);
		playvoiceover("VO\op7\MI1\shiplift",talker_ian);
		playvoiceover("VO\op7\MI1\shiplift2",talker_Rat);
		playvoiceover("VO\op7\MI1\shiplift2",talker_Ian);
		playvoiceover("VO\op7\MI1\uloser",talker_Rat);
		enableglobalinvulnerable;
		Playerai(epl_player0,true);
		StartMusic("Music\defeat",2.0,2.0,50);
		trans lost;
	endif;
	
	
	
	if (not generatordead) then
		
		if (timegreater(gametimer,480)) then
			killtheplayer = true;;
		endif;

		if (isdead(ebu_turret_power_generator)) then
			successobjective(eob_destroygenerator);
			numdone = numdone +1;
			playvoiceover("VO\op7\MI1\GenDead",talker_Ian);
			playlancematesound("VO\op7\MI1\GenDead",1);
			generatordead = true;
		endif;
	endif;
	
	if (not elitedead) then
		if (groupalldead(groupobjectid(30))) then
			successobjective(eob_stopeliteguard);
			numdone = numdone +1;
			if (numdone <> 3) then
				playvoiceover("VO\op7\MI1\ElitesDead",talker_Ian);
				playlancematesound("VO\op7\MI1\ElitesDead",1);
				setnavpoint(ena_Nav_gamma);
			endif;
			elitedead = true;
		endif;
	endif;
	
	if (not dropshipdead) then
		if (isdead(eve_hrothgar))  then
			//if ((isdead(eve_hrothgar)) or (isdead(eve_twinmechbay)))  then
			successobjective(eob_stopdropship);
			numdone = numdone +1;
			playvoiceover("VO\op7\MI1\4thout",talker_Rol);
			playlancematesound("VO\op7\MI1\dropdead",1);
			
			dropshipdead = true;
		endif;
	endif;
	
	
	if (not billyup) then
		if (numdone == 3) then
			billyup = true;
			setnavpoint(ena_nav_delta);
			playlancematesound("VO\op7\MI1\almostwin",1);
			playvoiceover("VO\op7\MI1\almostwin",talker_Ian);
			playvoiceover("VO\op7\MI1\almostwin",talker_Rat);
			playvoiceover("VO\op7\MI1\almostwin2",talker_Ian);
			playlancematesound("VO\op7\MI1\almostwin2",1);
			playvoiceover("VO\op7\MI1\getfixed",talker_Rat);
			playvoiceover("VO\op7\MI1\gorescue",talker_Rat);
			playvoiceover("VO\op7\MI1\goindelta",talker_Ian);
			playvoiceover("VO\op7\MI1\gogetfixed",talker_Ian);
			playlancematesound("VO\op7\MI1\gogetfixed",1);
			playvoiceover("VO\op7\MI1\seeyousoon",talker_Rat);
			playerai(epl_player0,true);
			revealobjective(eob_killwilliam);
			trans midcinema;
		endif;
	endif;
			
				
	if (moviedone) then
		if (isdead(eve_William_Dresari)) then
			if (generatordead) then
				endmission(true,57);
				successobjective(eob_killwilliam);
				playvoiceover("VO\op7\MI1\willdead",talker_Ian);
				playvoiceover("VO\op7\MI1\willdead",talker_Rat);
				playvoiceover("VO\op7\MI1\badelement",talker_Ian);
				playvoiceover("VO\op7\MI1\badelement",talker_Rat);
				//if (ismissioncomplete("content\missions\urban05_rescuesister\urban05_rescuesister.instance")) then
				//	playvoiceover("VO\op7\MI1\sisokay",talker_Rat);
				//	playvoiceover("VO\op7\MI1\sisokay",talker_Ian);
				//  playvoiceover("VO\op7\MI1\Willdo",talker_Rat);
				//endif;
				
				StartMusic("Music\victory2",3.0,3.0,50);
				
				enableglobalinvulnerable;
				settargetdesirability(playermech,-1);
				resettimer(gti_timer_28);
				starttimer(gti_timer_28);
				trans waitforcinema;
			endif;
		endif;
	endif;
	
//SOUNDS and music GO HERE

if (not entrancesoundplayed) then
	if (iswithin(playermech,ena_Nav_Alpha,70)) then
		playlancematesound("VO\op7\MI1\Enter",1);
		entrancesoundplayed = true;
	endif;
endif;

if (not elitesoundplayed) then
	if (boomboomstarted) then
		elitesoundplayed = true;
		playlancematesound("VO\op7\MI1\elites",2);
	endif;
endif;

if (not genclosesoundplayed) then
	if (iswithin(playermech,ebu_turret_power_generator,650)) then
		genclosesoundplayed = true;
		playlancematesound("VO\op7\MI1\genclose",1);
	endif;
endif;

if (not dropclosesoundplayed) then
	if (iswithin(playermech,eve_hrothgar,650)) then
		dropclosesoundplayed = true;
		playlancematesound("VO\op7\MI1\shipahead",2);
	endif;
endif;

if (not pleasplayed) then
	if ((timegreater(gti_timer_15,armortime)) or (skipit)) then
		if (isshot(eve_hrothgar)) then
			if ((getdamagerating(eve_hrothgar)) < ((armormult * dropshiparmor)/10)) then
			
				resettimer(gti_timer_15);
				starttimer(gti_timer_15);
				skipit = false;
				
				switch (pleastep)
	
				case 1:
				pleastep = pleastep +1;
				armortime = 10;
				armormult = 8;
				playvoiceover("VO\op7\MI1\20",talker_Rol);
				playvoiceover("VO\op7\MI1\20",talker_Ian);		
				endcase;
			
				case 2:
				pleastep = pleastep +1;
				armormult = 6;
				playvoiceover("VO\op7\MI1\50",talker_Rol);
				playvoiceover("VO\op7\MI1\50",talker_Ian);						
				endcase;	
				
				case 3:
				pleasplayed = true;
				playvoiceover("VO\op7\MI1\90",talker_Rol);
				playvoiceover("VO\op7\MI1\90",talker_Ian);
				endcase;				
								
				endswitch;
			
			endif;
		endif;
	endif;
endif;


//AMBIENT MUSIC
if (not ismusicplaying) then
	StartMusic("Music\urbanambloop",0.0,3.0,40);
endif;

//*************************************
//Navpoint Checks
//*************************************

if (not alphareached) then
	if (isnavrevealed(ena_nav_alpha)) then
		if (iswithin(playermech,ena_nav_alpha,checkrange)) then
			playvoiceover("VO\generic\navalpha",talker_Bet);
			alphareached = true;
		endif;
	endif;
endif;

if (not betareached) then
	if (isnavrevealed(ena_nav_beta)) then
		if (iswithin(playermech,ena_nav_beta,checkrange)) then
			playvoiceover("VO\generic\navbeta",talker_Bet);
			betareached = true;
		endif;
	endif;
endif;

if (not gammareached) then
	if (isnavrevealed(ena_nav_gamma)) then
		if (iswithin(playermech,ena_nav_gamma,checkrange)) then
			playvoiceover("VO\generic\navgamma",talker_Bet);
			gammareached = true;
		endif;
	endif;
endif;

if (not deltareached) then
	if (isnavrevealed(ena_nav_delta)) then
		if (iswithin(playermech,ena_nav_delta,checkrange)) then
			playvoiceover("VO\generic\navdelta",talker_Bet);
			deltareached = true;
		endif;
	endif;
endif;

//if (not epsilonreached) then
//	if (isnavrevealed(ena_nav_epsilon)) then
//		if (iswithin(playermech,ena_nav_epsilon,checkrange)) then
//			playvoiceover("VO\generic\navepsilon",talker_Bet);
//			epsilonreached = true;
//		endif;
//	endif;
//endif;

		

				
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state waitforcinema;
		
		code
		
		if (timegreater(gti_timer_28,4)) then
			playerai(epl_player0,true);
			trans won;
		endif;
		
		
endstate;


state opening;

	code
	
	if (cinemaskip) then
			cinema_cut = 0;
			resettimer (gti_Timer_21);
			killtimer(gti_timer_21);
			playerai(epl_player0,false);
			KillVoiceOvers;
			Fadefromblack(0.0);
			cinemaend;
			trans sit;	
	endif;

switch (cinema_cut)
			
			case 0:

			cinemastart;
			setactivecamera (eca_cameraship);
			Fadetoblack(0.0);

			Targetfollowobject(playermech);
			Camerafollowobject(playermech);
			SetCameraFootShake(playermech,100);

			targetoffset(0.0,7.0,0.0,0.0,false);
			Cameraoffset(-40.0,49.0,4.0,0.0,false);


			Cameraoffset(-90.0,35.0,12.0,30.0,false);

			startTimer (gti_Timer_21);
			resettimer (gti_Timer_21);

			cinema_cut = cinema_cut + 1;

			endcase;

			case 1:
				
				if (timegreater(gti_Timer_21,1)) then
					
					Fadefromblack(3.0);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 3;	
				endif;
				
			endcase;

			case 2:
				
				if (timegreater(gti_Timer_21,8)) then
					
					Fadetoblack(2.0);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;	
				endif;
				
			endcase;			

			case 3:
				
				if (timegreater(gti_Timer_21,2)) then
					Targetfollowobject(playermech);
					camerafollowobject(playermech);
					targetoffset(0.0,8.0,0.0,0.0,false);
					Cameraoffset(-100.0,49.0,35.0,0.0,true);
					Cameraoffset(90.0,15.0,35.0,17.0,true);
					Fadefromblack(2.0);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;	
				endif;
				
			endcase;
							
			case 4:
				
				if (timegreater(gti_Timer_21,32)) then
					cinema_cut = 0;
					resettimer (gti_Timer_21);
					killtimer(gti_timer_21);
					//Cameraoffset(0.0,0.0,0.0,0.0,false);
					playerai(epl_player0,false);
					cinemaend;
					trans sit;	
				endif;
				
			endcase;
			
		endswitch;

endstate;

state midcinema;

		var
		integer i;
		
		code
		
		switch cinema_cut
		
			case 0:
			
			
			
			cinemastart;
			setactivecamera (eca_cameraship);
			Fadetoblack(0.0);
			Fadefromblack(2.0);
			
			Targetfollowobject(eve_hrothgar);
			camerafollowobject(eve_hrothgar);
			SetCameraFootShake(playermech,100);
			targetoffset(0.0,15.0,0.0,0.0,false);
			Cameraoffset(-230.0,150.0,270.0,0.0,false);	
			
			Cameraoffset(120.0,45.0,-180.0,19.0,false);
			
			startTimer (gti_Timer_21);
			resettimer (gti_Timer_21);

			cinema_cut = cinema_cut + 1;
			StartMusic("Music\Aftermath",2.0,3.0,70);
			
			for i = 1 to 3 do
				if ((getlancemate(i) <> no_unit)) then
					if (not (isdead(getlancemate(i)))) then
						shutdown(getlancemate(i));
						teleporttohell((getlancemate(i)));
					endif;
				endif;			
			endfor;

			setgroupai((groupobjectid(1)),groupai_none);
			
			endcase;

			case 1:
				
				if (timegreater(gti_Timer_21,12)) then
					Targetfollowobject(playermech);
					camerafollowobject(playermech);
					targetoffset(0.0,10.0,0.0,0.0,false);
					Cameraoffset(-65.0,5.0,-50.0,0.0,false);
					Cameraoffset(-5.0,25.0,40.0,25.0,false);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;
				endif;
							
			endcase;

			case 2:
				
				if (timegreater(gti_Timer_21,23)) then
					
					Fadetoblack(3.0);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;	
				endif;
							
			endcase;

			case 3:
				
				if (timegreater(gti_Timer_21,5)) then
					Targetfollowobject(eve_William_Dresari);
					camerafollowobject(eve_William_Dresari);
					Fadefromblack(2.0);
					StartMusic("Music\ominous",0.0,0.0,70);
					playvoiceover("VO\op7\MI1\forgetsomething",talker_Wil);
					playvoiceover("VO\op7\MI1\forgetsomething",talker_Ian);
					playvoiceover("VO\op7\MI1\snafu",talker_Wil);
					playvoiceover("VO\op7\MI1\piper",talker_Wil);
					playvoiceover("VO\op7\MI1\where",talker_Ian);
					playvoiceover("VO\op7\MI1\where",talker_Wil);
					playvoiceover("VO\op7\MI1\letsgo",talker_Ian);
					playvoiceover("VO\op7\MI1\letsgo",talker_Wil);
					targetoffset(0.0,10.0,0.0,0.0,false);
					Cameraoffset(-65.0,5.0,-50.0,0.0,false);
					Cameraoffset(-5.0,25.0,40.0,15.0,false);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;	
				endif;
							
			endcase;

			case 4:
				
				if (timegreater(gti_Timer_21,12)) then
					Fadetoblack(2.0);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;
				endif;
							
			endcase;

			case 5:
				
				if (timegreater(gti_Timer_21,2)) then
					Targetfollowobject(playermech);
					camerafollowobject(playermech);
					Fadefromblack(2.0);
					targetoffset(0.0,10.0,0.0,0.0,false);
					Cameraoffset(-65.0,5.0,-50.0,0.0,false);
					Cameraoffset(-5.0,25.0,40.0,25.0,false);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;
				endif;
							
			endcase;
			
			case 6:
				
				if (timegreater(gti_Timer_21,7)) then
					Fadetoblack(2.0);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;
				endif;
							
			endcase;
			
			case 7:
				
				if (timegreater(gti_Timer_21,3)) then
					cinema_cut = 0;
					resettimer (gti_Timer_21);
					killtimer(gti_timer_21);
					//Cameraoffset(0.0,0.0,0.0,0.0,false);
					playerai(epl_player0,false);
					//fadefromblack(0.0);
					cinemaend;
					moviedone = true;
					trans sit;
				endif;
							
			endcase;
			
		endswitch;
	
endstate;

state won;

		code
		
	if (cinemaskip) then
			cinema_cut = 20;
			resettimer (gti_Timer_21);
			killtimer(gti_timer_21);
			endmission(true,1);	
			trans hang;
	endif;
		
		switch cinema_cut
		
			case 0:	
				CinemaStart;
				setactivecamera (eca_cameraship);
				//Fadetoblack(7.0);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Fadefromblack(2.0);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,20.0,75.0,0.0,false);
				Cameraoffset(150.0,250.0,45.0,40.0,false);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
			endcase;
			
		endswitch;
	
endstate;

state lost;

		code

	if (cinemaskip) then
			cinema_cut = 20;
			resettimer (gti_Timer_21);
			killtimer(gti_timer_21);
			endmission(false,1);
			trans hang;
	endif;
 	
		switch cinema_cut
		
			case 0:	
				CinemaStart;
				setactivecamera (eca_cameraship);
				//Fadetoblack(7.0);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Fadefromblack(2.0);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(0.0,20.0,75.0,0.0,false);
				Cameraoffset(60.0,125.0,25.0,20.0,false);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
			endcase;
			
		endswitch;	
endstate;

state hang;
		
		code
		
endstate;


endfsm.
//******************************************************************

